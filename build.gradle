plugins {
    id 'java'
    id 'maven'
}

group 'com.jfrog.ide'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile group: 'org.jfrog.buildinfo', name: 'build-info-extractor-npm', version: '2.13.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
    compile group: 'com.jfrog.xray.client', name: 'xray-client-java', version: '0.3.0'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.3'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    compile group: 'com.google.guava', name: 'guava', version: '27.1-jre'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
}

jar {
    manifest {
        attributes 'Implementation-Version': version
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

//noinspection GroovyAssignabilityCheck
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

test {
    useTestNG() {
        useDefaultListeners = true
    }
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        minGranularity 0
    }
}

artifacts {
    archives sourcesJar
}

