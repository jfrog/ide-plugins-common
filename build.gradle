plugins {
    id 'java'
    id 'maven'
}

group 'com.jfrog.ide'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenLocal()
    maven {
        url "https://oss.jfrog.org/artifactory/libs-snapshot"
        mavenContent {
            snapshotsOnly()
        }
    }
    maven {
        url "https://oss.jfrog.org/artifactory/libs-release"
    }
}

dependencies {
    compile group: 'org.jfrog.buildinfo', name: 'build-info-extractor-go', version: '2.22.x-SNAPSHOT'
    compile group: 'org.jfrog.buildinfo', name: 'build-info-extractor-npm', version: '2.22.x-SNAPSHOT'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.2'
    compile group: 'com.jfrog.xray.client', name: 'xray-client-java', version: '0.6.x-SNAPSHOT'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    testCompile group: 'org.testng', name: 'testng', version: '7.3.0'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
}

task uberJar(type: Jar) {
    archiveClassifier.set('uber')
    manifest {
        attributes 'Implementation-Version': archiveVersion
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    with jar
}

jar {
    manifest {
        attributes 'Implementation-Version': archiveVersion
    }
}

//noinspection GroovyAssignabilityCheck
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

test {
    useTestNG() {
        useDefaultListeners = true
    }
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        minGranularity 0
    }
}

artifacts {
    archives sourcesJar
    archives uberJar
}

