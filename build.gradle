plugins {
    id 'java'
    id 'maven'
}

group 'com.jfrog.ide'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    maven {
        url "http://oss.jfrog.org/artifactory/libs-snapshot"
        mavenContent {
            snapshotsOnly()
        }
    }
    maven {
        url "http://oss.jfrog.org/artifactory/libs-release"
    }
}

dependencies {
    compile group: 'org.jfrog.buildinfo', name: 'build-info-extractor-go', version: '2.18.x-SNAPSHOT'
    compile group: 'org.jfrog.buildinfo', name: 'build-info-extractor-npm', version: '2.18.x-SNAPSHOT'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'
    compile group: 'com.jfrog.xray.client', name: 'xray-client-java', version: '0.5.0'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    compile group: 'com.google.guava', name: 'guava', version: '28.1-jre'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
}

task uberJar(type: Jar) {
    archiveClassifier.set('uber')
    manifest {
        attributes 'Implementation-Version': version
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    with jar
}

jar {
    manifest {
        attributes 'Implementation-Version': version
    }
}

//noinspection GroovyAssignabilityCheck
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

test {
    useTestNG() {
        useDefaultListeners = true
    }
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        minGranularity 0
    }
}

artifacts {
    archives sourcesJar
    archives uberJar
}

