plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.jfrog.ide'

sourceCompatibility = '1.17'
targetCompatibility = '1.17'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://releases.jfrog.io/artifactory/oss-releases"
    }
    maven {
        url "https://releases.jfrog.io/artifactory/oss-snapshots"
    }
    maven {
        url "https://cache-redirector.jetbrains.com/intellij-dependencies"
    }
}

def buildInfoVersion = '2.41.13'
dependencies {
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.15.2'
    implementation group: 'org.jfrog.buildinfo', name: 'build-info-extractor-npm', version: buildInfoVersion
    implementation group: 'org.jfrog.buildinfo', name: 'build-info-extractor-go', version: buildInfoVersion
    implementation group: 'org.jfrog.buildinfo', name: 'build-info-client', version: buildInfoVersion
    implementation group: 'org.jfrog.buildinfo', name: 'build-info-api', version: buildInfoVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.2'
    implementation(group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13') {
        exclude group: 'commons-codec', module: 'commons-codec'
    }
    implementation group: 'com.jfrog.xray.client', name: 'xray-client-java', version: '0.14.1'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'org.jfrog.filespecs', name: 'file-specs-java', version: '1.1.0'
    implementation group: 'com.jetbrains.qodana', name: 'qodana-sarif', version: '0.2.113'
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.14'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    implementation group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.13'
    implementation group: 'com.jfrog', name: 'gradle-dep-tree', version: '3.0.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.14.0'
    implementation(group: 'com.opencsv', name: 'opencsv', version: '5.11.1') {
        exclude group: 'common-collections', module: 'commons-collections'
        exclude group: 'org.apache.commons', module: 'commons-text'
    }

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.2.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.2.0'
    testImplementation group: 'org.testng', name: 'testng', version: '7.7.0'
}

task uberJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier.set('uber')
    manifest {
        attributes 'Implementation-Version': archiveVersion
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    with jar
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Implementation-Version': archiveVersion
    }
}

//noinspection GroovyAssignabilityCheck
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

test {
    useTestNG() {
        useDefaultListeners = true
    }
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        minGranularity 0
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact uberJar
            artifact sourcesJar
        }
    }
}

